/*
===============================================================================================================================
                                    ATUALIZACOES SOFRIDAS DESDE A CONSTRUÇAO INICIAL
===============================================================================================================================
       Autor      |    Data    |                                             Motivo                                           |
------------------:------------:----------------------------------------------------------------------------------------------:
 Josué Danich     | 04/12/2015 | Revisão e normatização de fonte - Chamado 13081														|
===============================================================================================================================
*/

//====================================================================================================
// Definicoes de Includes da Rotina.
//====================================================================================================
#include "rwmake.ch"
#INCLUDE "PROTHEUS.CH"
#include "TOPCONN.ch"

/*
===============================================================================================================================
Programa----------: AEST029
Autor-------------: Erich Buttner
Data da Criacao---: 06/08/2013
===============================================================================================================================
Descrição---------: Cadastro de Tipos de Custo  - CHAMADO 3953  
===============================================================================================================================
Uso---------------: Italac
===============================================================================================================================
Parametros--------: Nenhum
===============================================================================================================================
Retorno-----------: Nenhum
===============================================================================================================================
Usuario-----------:
===============================================================================================================================
Setor-------------: TI
===============================================================================================================================
*/
User Function AEST029()


Private cArqInd 		:= CriaTrab(,.F.)		//Nome do arq. temporario
Private cCadastro 	:= "Tipos de Custo"
Private aRotina   	:= {}
Private aTitulos   	:= {}
Private _cAlias 		:= "ZL4"


Aadd(aRotina,{"Pesquisar" 	,"AxPesqui"  ,0,1 })
Aadd(aRotina,{"Visualizar"	,"AxVisual"  ,0,2 })
Aadd(aRotina,{"Incluir"		,"AxInclui"  ,0,3 })
Aadd(aRotina,{"Alterar"		,"AxAltera"  ,0,4 })
Aadd(aRotina,{"Excluir"		,"U_AEST029EX"  ,0,5 })

DbSelectArea("ZL4")
DbSetOrder(1) 

mBrowse( 6, 1,22,75,"ZL4")

Return(.T.)

/*
===============================================================================================================================
Programa----------: AEST029EX
Autor-------------: Erich Buttner
Data da Criacao---: 06/08/2013
===============================================================================================================================
Descrição---------: Cadastro de Tipos de Custo  - CHAMADO 3953  
===============================================================================================================================
Uso---------------: Italac
===============================================================================================================================
Parametros--------: 	_cAlias -> tabela aberta
						_nReg -> registro selecionado
						_nOpc -> opção de deleção
===============================================================================================================================
Retorno-----------: Nenhum
===============================================================================================================================
Usuario-----------:
===============================================================================================================================
Setor-------------: TI
===============================================================================================================================
*/
User Function AEST029EX(_cAlias,_nReg,_nOpc)

DbSelectArea("ZL5")
DbSetOrder(2)

If DbSeek(xFilial("ZL5")+ZL4->ZL4_COD)

	Alert( "Tipo Custo Já Amarrado a um Grupo de custo!!")
	Return .F.

Else

	AxDeleta(_cAlias,_nReg,_nOpc) 
	Return .T.

EndIf

Return


/*
===============================================================================================================================
Programa----------: AEST029G
Autor-------------: Josué Danich
Data da Criacao---: 04/12/2015
===============================================================================================================================
Descrição---------: Rotina para retornar próximo código de evento para campo ZL4->COD
===============================================================================================================================
Uso---------------: Italac
===============================================================================================================================
Parametros--------: 
===============================================================================================================================
Retorno-----------: _ccod - próximo valor para ZL4->COD
===============================================================================================================================
Usuario-----------:
===============================================================================================================================
Setor-------------: TI
===============================================================================================================================
*/
User Function AEST029G()

Local _ccod 	:= "000001"
Local _cquery := ""
Local _aarea	:= getarea()

_cquery	:= " SELECT " 
_cquery  	+= 	" MAX(ZL4_COD) AS CODI" 
_cquery  	+= 	" FROM " + RetSqlName("ZL4") + " ZL4 "
_cquery  	+= 	" WHERE ZL4_FILIAL = '" + xFilial("ZL4") + "'"
_cquery 	+= 	" AND   ZL4.D_E_L_E_T_ 	= ' '	"

TcQuery _cquery New Alias ZL4T
	
DBSelectArea("ZL4T")

If .not. ZL4T->( Eof() )

	_ccod := strzero( ( val( ZL4T->CODI) + 1 ),6 )

Endif

If Select("ZL4T") > 0 

	ZL4T->( DBCloseArea() )

EndIf

restarea(_aarea)

RETURN _ccod