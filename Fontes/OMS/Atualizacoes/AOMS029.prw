/*
===============================================================================================================================
               ULTIMAS ATUALIZAÇÕES EFETUADAS - CONSULTAR LOG DO VERSIONADOR PARA HISTORICO COMPLETO
===============================================================================================================================
       Autor      |    Data    |                                             Motivo                                           
-------------------------------------------------------------------------------------------------------------------------------
===============================================================================================================================
*/

#Include 'Protheus.Ch'
#Include 'FWMVCDef.Ch'

/*
===============================================================================================================================
Programa----------: AOMS029
Autor-------------: Alexandre Villar
Data da Criacao---: 13/08/2014
===============================================================================================================================
Descrição---------: Cadastro das Metas de Venda para Leite Magro para validação do adicional de comissão
===============================================================================================================================
Parametros--------: Nenhum
===============================================================================================================================
Retorno-----------: Nenhum
===============================================================================================================================
*/
User Function AOMS029()

Local oBrowse		:= Nil

//-- Instancia a classe do Browse --//
oBrowse := FWMBrowse():New()
oBrowse:SetAlias('ZC0')
oBrowse:SetDescription( 'Metas - Vendas Leite Magro' )
oBrowse:Activate()

Return()

/*
===============================================================================================================================
Programa----------: MenuDef
Autor-------------: Alexandre Villar
Data da Criacao---: 13/08/2014
===============================================================================================================================
Descrição---------: Rotina de definição do menu da tela principal
===============================================================================================================================
Parametros--------: Nenhum
===============================================================================================================================
Retorno-----------: Nenhum
===============================================================================================================================
*/
Static Function MenuDef()

Local _aRotina := {}

ADD OPTION _aRotina Title 'Configurar'	Action 'U_AOMS029C(1)' OPERATION 4 ACCESS 0
ADD OPTION _aRotina Title 'Incluir'		Action 'U_AOMS029C(2)' OPERATION 3 ACCESS 0

Return( _aRotina )

/*
===============================================================================================================================
Programa----------: AOMS029C
Autor-------------: Alexandre Villar
Data da Criacao---: 13/08/2014
===============================================================================================================================
Descrição---------: Rotina para a reconfiguração de Metas já existentes
===============================================================================================================================
Parametros--------: Nenhum
===============================================================================================================================
Retorno-----------: Nenhum
===============================================================================================================================
*/

User Function AOMS029C( _nOpc )

Local _bOk     := {|| AOMS029GRV( _nOpc , _cDtRef , _nMeta ) , _oDlg:End() }
Local _bCancel := {|| _oDlg:End() }
Local _cDtRef  := '    /  '
Local _nMeta   := 0
Local _cTitAux := 'Inclusão'
Local _oDlg    := Nil

If !U_ITVLDUSR(5)
	u_itmsg(  'Usuário sem acesso à alterar o cadastro de Configuração de Metas! Verifique com a área de TI/ERP' , 'Atenção!' , , 1 )
	Return()
EndIf

If _nOpc == 1
	
	_cDtRef  := SubStr( ZC0->ZC0_ANOMES , 1 , 4 ) +'/'+ SubStr( ZC0->ZC0_ANOMES , 5 , 2 )
	_nMeta   := ZC0->ZC0_META
	_cTitAux := 'Alteração'
	
EndIf

DEFINE MSDIALOG _oDlg TITLE "Cadastro de Metas - "+ _cTitAux FROM 000,000 TO 100,500 PIXEL
	
	@020,010 SAY 'Ano/Mes:'                                   OF _oDLg PIXEL
	@018,040 MSGET _cDtRef PICTURE "@E 9999/99"  SIZE 050,010 OF _oDlg PIXEL
	@020,120 SAY '% Meta:'                                    OF _oDLg PIXEL
	@018,150 MSGET _nMeta  PICTURE "@E 999.99"   SIZE 050,010 OF _oDlg PIXEL
	
ACTIVATE MSDIALOG _oDlg ON INIT ( EnchoiceBar( _oDlg , _bOk , _bCancel ) ) CENTERED

Return()

/*
===============================================================================================================================
Programa----------: AOMS029C
Autor-------------: Alexandre Villar
Data da Criacao---: 13/08/2014
===============================================================================================================================
Descrição---------: Rotina para a reconfiguração de Metas já existentes
===============================================================================================================================
Parametros--------: Nenhum
===============================================================================================================================
Retorno-----------: Nenhum
===============================================================================================================================
*/

Static Function AOMS029GRV( _nOpc , _cDtRef , _nMeta )

If _nOpc == 1
	ZC0->( RecLock( 'ZC0' , .F. ) )
Else
	ZC0->( RecLock( 'ZC0' , .T. ) )
EndIf

ZC0->ZC0_ANOMES := StrTran( _cDtRef , '/' , '' )
ZC0->ZC0_META   := _nMeta

ZC0->( MsUnLock() )

Return()