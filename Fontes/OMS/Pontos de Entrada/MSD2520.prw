/*
===============================================================================================================================
               ULTIMAS ATUALIZAÇÕES EFETUADAS - CONSULTAR LOG DO VERSIONADOR PARA HISTORICO COMPLETO
===============================================================================================================================
       Autor      |    Data    |                                             Motivo                                           
-------------------------------------------------------------------------------------------------------------------------------
Alexandre Villar  | 23/03/2016 | Ajuste para padronizar a utilização de rotinas de consultas customizadas. Chamado 14774
-------------------------------------------------------------------------------------------------------------------------------
Lucas Borges      | 11/10/2019 | Removidos os Warning na compilação da release 12.1.25. Chamado 28346
-------------------------------------------------------------------------------------------------------------------------------
Lucas Borges      | 07/05/2021 | Corrigida chamada de parâmetro. Chamado 36469
===============================================================================================================================
*/

//====================================================================================================
// Definicoes de Includes da Rotina.
//====================================================================================================
#Include 'Protheus.ch'

/*
===============================================================================================================================
Programa--------: MSD2520
Autor-----------: Jeovane
Data da Criacao-: 31/07/2009
===============================================================================================================================
Descrição-------: P.E. no momento da exclusão da Nota Fiscal de saída.
===============================================================================================================================
Parametros------: Nenhum
===============================================================================================================================
Retorno---------: Nenhum
===============================================================================================================================
*/
User Function MSD2520()         

If SF2->F2_TIPO == "N"
	ExclSt()
EndIf

Return

/*
===============================================================================================================================
Programa--------: ExclST
Autor-----------: Guilherme Gesualdo
Data da Criacao-: 10/09/2012
===============================================================================================================================
Descrição-------: Funcao usada para verificar se existe titulos ST para a nota. Se houver faz a exclusao dos titulos. 
===============================================================================================================================
Parametros------: Nenhum
===============================================================================================================================
Retorno---------: Nenhum
===============================================================================================================================
*/
Static Function ExclST()

Local _aArea	 := GetArea()
Local _cSerie    := SF2->F2_SERIE
Local _cDoc      := SF2->F2_DOC
Local _cCodCli   := SF2->F2_CLIENTE
Local _cLojCli   := SF2->F2_LOJA

Local _cFORST 	:= AllTrim(SuperGetMV("IT_STFORN",.F.,""))
                   
Local _aSE1Exc := {}
Local _aSE2Exc := {}

Local nModAnt  := nModulo
Local cModAnt  := cModulo

DBSelectArea("SE2")
dbSetOrder(1) //E2_FILIAL+E2_PREFIXO+E2_NUM+E2_PARCELA+E2_TIPO+E2_FORNECE+E2_LOJA 

If DbSeek(xFilial("SE2")+_cSerie+_cDoc+SPACE(2)+'ICM'+SUBSTR(_cFORST,1,6)+SUBSTR(_cFORST,7,4))
				
	lMsErroAuto := .f.
							
	_aSE2Exc  := {	{"E2_FILIAL"  ,SE2->E2_FILIAL          	,Nil},;
					{"E2_PREFIXO" ,SE2->E2_PREFIXO         	,Nil},;
					{"E2_NUM"	  ,SE2->E2_NUM     	      	,Nil},;
					{"E2_TIPO"	  ,SE2->E2_TIPO    	     	,Nil},;
					{"E2_FORNECE" ,SE2->E2_FORNECE       	,Nil},;
					{"E2_LOJA"	  ,SE2->E2_LOJA         	,Nil},;
					{"E2_ORIGEM"  ,SE2->E2_ORIGEM       	,Nil}}       
		
	//====================================================================================================
	// Altera o modulo para Financeiro, senao o SigaAuto nao executa.
	//====================================================================================================
	nModulo := 6
	cModulo := "FIN"
							
	MSExecAuto({|x,y,z| Fina050(x,y,z)},_aSE2Exc,,5)
							
	//====================================================================================================
	// Restaura o modulo em uso.
	//====================================================================================================
	nModulo := nModAnt
	cModulo := cModAnt
							
	If lMsErroAuto
		Mostraerro() 
	Endif
		
EndIf
		
//====================================================================================================
// Exclui Título a Receber em nome do Cliente
//====================================================================================================
dbSelectArea("SE1")
dbSetOrder(2) //E1_FILIAL+E1_CLIENTE+E1_LOJA+E1_PREFIXO+E1_NUM+E1_PARCELA+E1_TIPO    

If DbSeek(xFilial("SE1")+_cCodCli+_cLojCli+_cSerie+_cDoc+SPACE(2)+'ICM')
				
	lMsErroAuto := .f.
						
	_aSE1Exc  := {	{"E1_FILIAL"  ,SE1->E1_FILIAL    		,Nil},;
    	          	{"E1_CLIENTE" ,SE1->E1_CLIENTE      	,Nil},;
					{"E1_LOJA"	  ,SE1->E1_LOJA 	     	,Nil},;
    	          	{"E1_PREFIXO" ,SE1->E1_PREFIXO   		,Nil},;
					{"E1_NUM"	  ,SE1->E1_NUM     	     	,Nil},;
					{"E1_TIPO"	  ,SE1->E1_TIPO           	,Nil},;
					{"E1_ORIGEM"  ,SE1->E1_ORIGEM         	,Nil}}      
	
	//====================================================================================================
	// Altera o modulo para Financeiro, senao o SigaAuto nao executa.
	//====================================================================================================
	nModulo := 6
	cModulo := "FIN"
						
	MSExecAuto({|x,y| Fina040(x,y)},_aSE1Exc,5)
						
	//====================================================================================================
	// Restaura o modulo em uso.
	//====================================================================================================
	nModulo := nModAnt
	cModulo := cModAnt
						
	If lMsErroAuto
		Mostraerro() 
	Endif
			
EndIf

//====================================================================================================
// Restaura as áreas posicionadas
//====================================================================================================
RestArea( _aArea )

Return
